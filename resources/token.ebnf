Token = Punctuator | Name | IntValue | FloatValue | StringValue | BooleanValue | NullValue
Punctuator = "!" | "$" | "(" | ")" | "..." | ":" | "=" | "@" | "[" | "]" | "{" | "|" | "}"
Name = #"[_A-Za-z][_0-9A-Za-z]*"
IntValue = IntegerPart
IntegerPart = (NegativeSign? "0") | (NegativeSign? NonZeroDigit Digit*)
Digit = #"[0-9]"
NonZeroDigit = #"[1-9]"
NegativeSign = "-"
FloatValue = (IntegerPart FractionalPart) |
             (IntegerPart ExponentPart) |
             (IntegerPart FractionalPart ExponentPart)
FractionalPart = (<#"\."> Digit*)
ExponentPart = (ExponentIndicator Sign? Digit*)
ExponentIndicator = "e" | "E"
Sign = "+" | "-"
StringValue = (Quote StringCharacters Quote) |
              (BlockQuoteOpen BlockStringCharacters BlockQuoteClose)
StringCharacters = StringCharacter*
BlockStringCharacters = BlockStringCharacter*
Quote = <"\"">
BlockQuoteOpen = <BlockQuote>
BlockQuoteClose = <BlockQuote>
BlockQuote = <"\"\"\"">
StringCharacter = #"[\u0009\u0020-\u0021\u0023-\u005B\u005D-\uFFFF]" |
                  ("\\u" EscapedUnicode) |
                  ("\\" EscapedCharacter)
EscapedUnicode = #"[0-9A-Fa-f]{4}"
EscapedCharacter = "\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t"
BlockStringCharacter = #"([\u0009\u000A\u000D\u0020-\u0021\u0023-\uFFFF]|(\"\"?(?!\")))" |
                       <'\\'> '\"\"\"'
(* not included as lexical tokens in June 2018 edition *)
BooleanValue = 'true' | 'false'
NullValue = <'null'>
Colon = ':'
BraceOpen = '{'
BraceClose = '}'
BracketOpen = '['
BracketClose = ']'
ExclamationMark = '!'
Equals = '='
ParensOpen = '('
ParensClose = ')'
PipeCharacter = '|'
Ellipsis = '...'
